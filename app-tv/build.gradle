apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"
    def currentVersionCode = getVersionCode()
    def currentVersionName = getVersionName()

    defaultConfig {
        applicationId "com.vodbyte.freetv"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode currentVersionCode
        versionName "3." + currentVersionName
        flavorDimensions "versionCode"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi','armeabi-v7a','arm64-v8a' ,'x86','x86_64'
        }

    }

    productFlavors {

        devTv {
            applicationId "app.xxx.devtv"
            resValue("string", "app_name", "TV开发调试")
            buildConfigField("String", "UMENG_APPKEY", '""')
            buildConfigField("String", "BASE_API_URL", '"http://www.xxx.com"')
        }

    }

    signingConfigs {
        config {
            keyAlias ''
            keyPassword ''
            storeFile file('../vodbyte.jks')
            storePassword ''
        }

        debug {
            v1SigningEnabled true
            v2SigningEnabled true
        }
        release {
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.config
            buildConfigField("boolean", "isDebug", "true")
        }

        release {
            minifyEnabled true
            shrinkResources true //去除无用资源
            zipAlignEnabled true   //是否启用zipAlign压缩
            signingConfig signingConfigs.config
            buildConfigField("boolean", "isDebug", "false")
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    sourceSets {
        main {
            res.srcDirs = [
                "src/main/res",
                "src/main/res-pic",
                "src/main/res-icon",
                "src/main/res-fit",
                "src/main/res-launcher"
            ]
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

//    applicationVariants.all { variant ->
//        variant.outputs.all { output->
//            outputFileName = "../../../../../../outputs/tv_${variant.name}_${variant.applicationId}_${variant.versionName}_${variant.versionCode}.apk"
//        }
//    }

}

def getVersionName() {
    def date = new Date();
    def versionName = date.format('MMdd');
    return versionName
}

def getVersionCode() {// 获取版本号
    def versionFile = file('version.properties')// 读取第一步新建的文件
    if (versionFile.canRead()) {// 判断文件读取异常
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionFile))
        def versionCode = versionProps['VERSION_CODE'].toInteger()// 读取文件里面的版本号
        versionProps['VERSION_CODE'] = (++versionCode).toString()
        versionProps.store(versionFile.newWriter(), null)
        return versionCode // 返回自增之后的版本号
    } else {
        throw new GradleException("Could not find version.properties!")
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.material:material:1.3.0'
//    implementation 'com.android.support:appcompat-v7:30.0.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.github.ybq:Android-SpinKit:1.4.0'


    implementation project(path: ':easy_rv_adapter')
    implementation project(path: ':dkplayer-ui')
    implementation project(path: ':ecloadinglib')
    implementation project(path: ':dkplayer-videocache')
    implementation project(path: ':dkplayer-java')
    implementation project(path: ':dkplayer-players:ijk')
    implementation project(path: ':dkplayer-players:exo')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
//    implementation 'com.android.support:cardview-v7:30.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    // 异步处理
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    //在线更新
    implementation 'com.github.AlexLiuSheng:CheckVersionLib:2.4.1_androidx'

    // 依赖注入
    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    // 网络访问
    //implementation 'com.squareup.okhttp3:okhttp:4.3.1'
    // 3.12.x 兼容android4.4
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    //强制指定该project下的用着okhttp库的module统一使用指定版本
    configurations.all {
        resolutionStrategy.force 'com.squareup.okhttp3:okhttp:3.12.1'
    }

    // 图片加载
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:1.5.0'

    //弹幕库
    implementation 'com.github.ctiao:DanmakuFlameMaster:0.9.25'
    // 事件传递
    implementation 'org.greenrobot:eventbus:3.2.0'
    // JSON解析
    implementation 'com.google.code.gson:gson:2.8.7'
    // 日志收集
    implementation 'com.tencent.bugly:crashreport:3.3.92'
/*    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'*/
    // 数据库
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:2.1.0'
    // 分包
    implementation 'androidx.multidex:multidex:2.0.0'
    // 流光效果
    /*implementation 'com.github.nwhhades:NiceView:1.0'*/
    // 自动调整大小
    implementation 'me.grantland:autofittextview:0.2.1'
    // 上拉刷新下拉加载
    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.0.5.1'
    // 二维码
    implementation 'com.google.zxing:core:3.3.2'
    // 加载动画
    implementation 'com.github.zzz40500:android-shapeLoadingView:1.0.3.2'
    // 数据库
    implementation 'org.litepal.android:java:3.0.0'
    // 友盟
    implementation "com.umeng.umsdk:common:9.4.0"
    implementation 'com.umeng.umsdk:asms:1.2.3'// 必选
}
